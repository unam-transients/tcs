#!/bin/sh

########################################################################

# This file is part of the UNAM telescope control system.

########################################################################

# Copyright Â© 2014, 2016, 2017, 2019 Alan M. Watson <alan@astro.unam.mx>
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL
# WARRANTIES WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE
# AUTHOR BE LIABLE FOR ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL
# DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR
# PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE OR OTHER
# TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
# PERFORMANCE OF THIS SOFTWARE.

########################################################################

#\
umask 0; exec /usr/bin/tclsh8.6 -encoding "utf-8" "$0" ${1+"$@"}


proc getdirectoryname {} {
  return "/var/cryostat"
}

proc getfilename {} {
  set filenames [glob -nocomplain -directory [getdirectoryname] "*"]
  set filemtimesandnames {}
  foreach filename $filenames {
    lappend filemtimesandnames [list [file mtime $filename] $filename]
  }
  set filemtimesandnames [lsort -decreasing -integer -index 0 $filemtimesandnames]
  set filemtimeandname [lindex $filemtimesandnames 0]
  set filename [lindex $filemtimeandname 1]
  return $filename
}

proc checktemperature {} {

  set filename [getfilename]
  
  set channel [open "|/usr/bin/tail -1 $filename" "r"]
  set line [gets $channel]
  close $channel

  if {[scan $line "%*s %*s %*s %*s %f" temperature] != 1} {
    return [list 1 "unknown"]
  }

  set temperature [format "%.3f" $temperature]

  set when [file mtime $filename]
  set now [clock seconds]
  if {$now - $when > 120} {
    return [list 1 "stale"]
  } elseif {$temperature >= 400.0} {
    return [list 0 "unreliable"]
  } elseif {$temperature >= 50.0} {
    return [list 2 "$temperature K"]
  } else {
    return [list 0 "$temperature K"]
  }
}

set result     [checktemperature]
set status     [lindex $result 0]
set message    [lindex $result 1]

switch $status {
  0 {
    set statustext "OK"
  }
  1 {
    set statustext "WARNING"
  }
  2 {
    set statustext "CRITICAL"
  }
}

puts "CRYOSTAT TEMPERATURE $statustext - $message."
exit $status
